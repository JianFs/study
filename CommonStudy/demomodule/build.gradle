if (Boolean.parseBoolean(IS_DEBUG_DEMO_MODULE)) {
    apply plugin: 'com.android.application'
} else {
    apply plugin: 'com.android.library'
}
//Kotlin
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
//GreenDao
apply plugin: 'org.greenrobot.greendao'

android {
    compileSdkVersion COMPILE_SDK_VERSION

    defaultConfig {
        minSdkVersion MIN_SDK_VERSION
        targetSdkVersion TARGET_SDK_VERSION

        versionCode Integer.parseInt(VERSION_CODE)
        versionName VERSION_NAME

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"

        multiDexEnabled true

        resourcePrefix "demo_"

        //aRouter配置
        kapt {
            arguments {
                arg("moduleName", project.getName())
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            if (Boolean.parseBoolean(IS_DEBUG_DEMO_MODULE)) {
                manifest.srcFile 'src/main/debug/AndroidManifest.xml'
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
                //集成开发模式下排除debug文件夹中的所有Java文件
                java {
                    exclude 'debug/**'
                }
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    //Test
    implementation 'com.android.support.constraint:constraint-layout:1.0.2'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.1'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.1'

    implementation project(':centre')

    //运行时注解处理
    kapt "com.github.bumptech.glide:compiler:$GLIDE_VERSION"
    kapt "com.alibaba:arouter-compiler:$AROUTER_COMPILER_VERSION"
}
